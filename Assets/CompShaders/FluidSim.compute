// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain
#pragma kernel UpdateDensities
#pragma kernel CalculateVelocities
#pragma kernel SetPredictedPosition

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture

struct CFluidParticle
{
    uint id;
    float3 position;
    float3 velocity;
    float density;
    float3 predictedPosition;
    int2 spartialPosition;
};

static const int Poly = 0;
static const int Spiky = 1;
static const int PolySpiky = 2;
static const int SpikyPoly = 3;


static const int threadSize = 64;
static const float2 float2_zero = float2(0, 0);
static const float2 float2_one = float2(1.0, 1.0);
static const float3 float3_zero = float3(0, 0, 0);
static const float3 float3_one = float3(1.0, 1.0, 1.0);
static const float pi = 3.141592654;
static const float mass = 1;
float2 borderBounds;
float2 currentCellSize;
float2 gridPosition;
float2 gridSize;
float deltaTime;
float size;
float gravity;
float friction;
float smoothingLength;
float restDensity;
float stiffness;
float kinematicViscosity;
int particlesCount;
float maxSpeed;
int kernelType;

RWStructuredBuffer<float3> pressureForces;
RWStructuredBuffer<CFluidParticle> particles;
RWStructuredBuffer<float4x4> particleMatrices;

float3 ValidateVector(float3 v)
{
    if (isnan(v.x) || isnan(v.y) || isnan(v.z) ||
        isinf(v.x) || isinf(v.y) || isinf(v.z))
    {
        return float3_zero;
    }
    return v;
}

float PolyKernel(float l, float r)
{
    if (r < 0 || r > 1)
        return 0;

    float alpha = 4 / (pi * pow(l, 8));
    float volume = pow((l * l - r * r), 3);
    return alpha * volume;
}

float PolyKernelDerivative(float l, float r)
{
    if (r < 0 || r > 1)
        return 0;

    float alpha = 4 / (pi * pow(l, 8));
    float volume = 3 * pow((l * l - r * r), 2);
    return -alpha * volume;
}

float SpikyKernel(float l, float r)
{
    if (r < 0 || r > 1)
        return 0;

    float alpha = 10 / (pi * pow(l, 5));
    float volume = pow((l - r), 3);
    return alpha * volume;
}

float SpikyKernelDerivative(float l, float r)
{
    if (r < 0 || r > 1)
        return 0;

    float alpha = 10 / (pi * pow(l, 5));
    float volume = 3 * pow((l - r), 2);
    return -alpha * volume;
}

float ViscosityKernel(float radius, float dst)
{
    if (dst >= radius)
        return 0.0;

    float alpha = 45.0 / (pi * pow(radius, 6.0));
    float volume = radius - dst;

    return alpha * volume;
}

float CalculateDensity(inout CFluidParticle particle)
{ 
    float density = 0;
    
    for (int i = 0; i < particlesCount; i++)
    {
        if (i == (int) particle.id)
            continue;
        CFluidParticle neighbor = particles[i];
        float dst = length(neighbor.predictedPosition - particle.predictedPosition);
        if (dst <= smoothingLength)
        {
            float influence = 0;
            if (kernelType == Poly)
            {
                influence = PolyKernel(smoothingLength, dst);
            }
            else if (kernelType == Spiky)
            {
                influence = SpikyKernel(smoothingLength, dst);
            }
            else if (kernelType == PolySpiky)
            {
                influence = PolyKernel(smoothingLength, dst);
            }
            else if (kernelType == SpikyPoly)
            {
                influence = SpikyKernel(smoothingLength, dst);
            }
            density += mass * influence;
        }
    }
    
    return density;
}


void KeepInBounds(inout CFluidParticle particle)
{
    float2 halfBounds = borderBounds / 2.0 - float2_one * size / 2.0;
    
    // Check boundaries for X-axis
    if (abs(particle.position.x) > halfBounds.x)
    {
        particle.velocity.x *= -1.0f * friction; // Reverse velocity
        particle.position.x = halfBounds.x * sign(particle.position.x); // Clamp position
    }

    // Check boundaries for Y-axis
    if (abs(particle.position.y) > halfBounds.y)
    {
        particle.velocity.y *= -1.0f * friction; // Reverse velocity
        particle.position.y = halfBounds.y * sign(particle.position.y); // Clamp position
    }
}

float ConvertDensityToPressure(float density)
{
    float densityError = density - restDensity;
    float pressure = densityError * stiffness;
    return pressure;
}

float3 CalculatePressureForce(inout CFluidParticle particle)
{
    float3 pressureForce = float3_zero;
    
    float particlePressure = ConvertDensityToPressure(particle.density) / pow(particle.density, 2);
    
    for (int i = 0; i < particlesCount; i++)
    {
        if (i == (int) particle.id)
            continue;
        CFluidParticle neighbor = particles[i];
        float3 offset = neighbor.predictedPosition - particle.predictedPosition;
        float dst = length(offset);
        if (dst <= smoothingLength)
        {
            float slope = 0;
            if (kernelType == Poly)
            {
                slope = PolyKernelDerivative(smoothingLength, dst);
            }
            else if (kernelType == Spiky)
            {
                slope = SpikyKernelDerivative(smoothingLength, dst);
            }
            else if (kernelType == PolySpiky)
            {
                slope = SpikyKernelDerivative(smoothingLength, dst);
            }
            else if (kernelType == SpikyPoly)
            {
                slope = PolyKernelDerivative(smoothingLength, dst);
            }
            float3 direction = normalize(offset);
            float neighborPressure = max(ConvertDensityToPressure(neighbor.density) / pow(neighbor.density, 2), 0);
            float sharedPressure = particlePressure + neighborPressure;
            
            float3 result = direction * sharedPressure * slope * mass;
            
            pressureForce += ValidateVector(result);
        }
    }
    
    return pressureForce;
}

float3 CalculateViscosityForce(inout CFluidParticle particle)
{
    float3 laplacianVelocity = float3_zero;

    for (int i = 0; i < particlesCount; i++)
    {
        if (i == (int) particle.id)
            continue;
        
        CFluidParticle neighbor = particles[i];
        
        float3 r = neighbor.predictedPosition - particle.predictedPosition;
        float3 r_vel = neighbor.velocity - particle.velocity;
        float r_mag = length(r);
        float r2 = pow((r_mag), 2);

        float3 gradient = r * ViscosityKernel(smoothingLength, r_mag);
        float arOd = dot(r, r_vel) / r2;
        float mOd = mass / neighbor.density;
        
        
        float3 result = mOd * arOd * gradient;
        
        laplacianVelocity += ValidateVector(result);
    }
    
    return kinematicViscosity * laplacianVelocity;
}

float3 ClampVelocity(float3 velocity)
{
    float speed = length(velocity);

        // Clamp the magnitude of the velocity vector between 0 and maxSpeed
    if (speed > maxSpeed)
    {
        velocity = normalize(velocity) * maxSpeed;
    }
    else if (speed < -maxSpeed)
    {
        velocity = normalize(velocity) * -maxSpeed;
    }

    return velocity;
}

[numthreads(threadSize, 1, 1)]
void SetPredictedPosition(uint3 id : SV_DispatchThreadID)
{
    uint index = id.x;
    
    if ((int) index >= particlesCount)
        return;
    float3 accel = particles[index].velocity * deltaTime;
    particles[index].predictedPosition = particles[index].position + ValidateVector(accel);
}

[numthreads(threadSize, 1, 1)]
void UpdateDensities(uint3 id : SV_DispatchThreadID)
{
    uint index = id.x;
    
    if ((int) index >= particlesCount)
        return;
    particles[index].density = CalculateDensity(particles[index]);   
}

[numthreads(threadSize, 1, 1)]
void CalculateVelocities(uint3 id : SV_DispatchThreadID)
{
    uint index = id.x;
    
    if ((int) index >= particlesCount)
        return;
    
    float3 pressureForce = CalculatePressureForce(particles[index]);
    float3 viscosityForce = CalculateViscosityForce(particles[index]);
    float3 gravityForce = float3(0, -1, 0) * gravity;
    float3 totalForce = pressureForce + viscosityForce + gravityForce;
    float3 acceleration = totalForce / particles[index].density;
    particles[index].velocity += acceleration * deltaTime;
}

[numthreads(threadSize, 1, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    // TODO: insert actual code here!
    uint index = id.x;
    if ((int) index >= particlesCount)
        return;

    float3 velocity = ClampVelocity(particles[index].velocity);
    velocity =  ClampVelocity(velocity); 
    velocity = ValidateVector(velocity);
    
    particles[index].velocity = velocity;
    
    float3 accel = particles[index].velocity * deltaTime;
    accel = ValidateVector(accel);
    
    particles[index].position += accel;
    
    KeepInBounds(particles[index]);
    
    float4x4 resMatrix = float4x4(
        size, 0, 0, particles[index].position.x,
        0, size, 0, particles[index].position.y,
        0, 0, size, particles[index].position.z,
        0, 0, 0, 1
    );
   
    particleMatrices[index] = resMatrix;
}
